１．フォルダ構成を作成する。
　・bat
　・data
　・exe
　・log
 　　・BKUP
　・sql
　・vbs
２．batフォルダ配下に実行するbatファイルを作成する。
　・SET_ENV.BAT  →共通用のバッチファイル作成
 
/////////////////////////////////////サンプルソース：開始////////////////////////////////////////////
@echo off
rem ***************************************************************************
rem システム名   : XXX環境構築
rem モジュール名 : SET_ENV.BAT
rem 処理名       : 環境変数の設定処理
rem 注意事項     : BATスクリプトの先頭でcall
rem
rem Date        P-No      所属/氏名                内   容
rem ---------------------------------------------------------------------------
rem 2012/XX/XX  P-XXXXXX  XXX/XXXX                新規作成
rem yyyy/mm/dd  P-XXXXXX  ＃＃＃＃＃＃＃＃        　変更
rem ***************************************************************************
rem
rem ===========================================================================
rem ディレクトリ定義
rem ---------------------------------------------------------------------------
rem set DRIVE=c:\bat
set DRIVE=D:\bat
set BAT_PATH=%DRIVE%\bat\
set EXE_PATH=%DRIVE%\exe\
set LOG_PATH=%DRIVE%\log\
set LOGBK_PATH=%DRIVE%\log\BKUP\
rem
rem ===========================================================================
rem その他の定義
rem ---------------------------------------------------------------------------
set LINE01=***************************************************************************
set LINE02============================================================================
rem ---------------- End Of Batch ---------------------------------------------

/////////////////////////////////////サンプルソース：終了////////////////////////////////////////////

　・XXX.BAT  →処理用のバッチファイル作成
 
 /////////////////////////////////////サンプルソース：開始////////////////////////////////////////////
 
 @echo off

rem ***************************************************************************
rem システム名   : XXX機能構築
rem モジュール名 : XXX.BAT
rem 処理名       : XXX処理
rem 注意事項     : 
rem
rem Date        P-No      所属/氏名                内   容
rem ---------------------------------------------------------------------------
rem 2012/XX/XX  P-XXXXXX  XXX/XXXX                新規作成
rem yyyy/mm/dd  P-XXXXXX  ＃＃＃＃＃＃＃＃        　変更
rem ***************************************************************************

rem ---------------- START Of Batch -------------------------------------------
setlocal

rem ===========================================================================
rem 環境変数の設定
rem ---------------------------------------------------------------------------
:USR_PATH
rem call %~p0\SET_ENV.BAT
call %~dp0\SET_ENV.BAT

set BAT_ID=%~n0
set STATUS=0

rem ===========================================================================
rem Batch Start 処理
rem ---------------------------------------------------------------------------
:START
set LOGFILE=%LOG_PATH%%BAT_ID%.LOG
set time2=%time: =0%
set LOG_BAK_NAME=%date:~-10,4%%date:~-5,2%%date:~-2,2%%time2:~0,2%%time2:~3,2%%time2:~6,2%

copy /y %LOGFILE% %LOGBK_PATH%%BAT_ID%_%LOG_BAK_NAME%.LOG > nul

del %LOGFILE% 2> nul

echo %LINE01% >> %LOGFILE%
echo %BAT_ID% %BAT_PATH%%BAT_ID%.BAT START >> %LOGFILE%
echo %date% %time% >> %LOGFILE%
echo %LINE02% >> %LOGFILE%

rem ===========================================================================
rem Batch Main 処理
rem ---------------------------------------------------------------------------
echo ログ変数出力 → 開始 >> %LOGFILE%
echo ログファイルパス=%LOGFILE% >> %LOGFILE%
echo バックアップログファイルパス=%LOGBK_PATH%%BAT_ID%_%LOG_BAK_NAME%.LOG >> %LOGFILE%
echo ログ変数出力 → 終了 >> %LOGFILE%

echo %date% %time% %EXE_PATH%%BAT_ID%.exeの起動 → 開始 >> %LOGFILE%
rem XXX.exeモジュールを呼び出す(※引数の設定が必要)
echo 引数１：%~1を設定 >> %LOGFILE%
call %EXE_PATH%%BAT_ID%.exe %~1
set STATUS=%ERRORLEVEL%
echo %date% %time% %EXE_PATH%%BAT_ID%.exeの起動 → 終了(%STATUS%) >> %LOGFILE%
if %STATUS% GEQ 1 GOTO END

rem ===========================================================================
rem Batch End 処理
rem ---------------------------------------------------------------------------
:END
echo %LINE02% >> %LOGFILE%
echo %BAT_ID% %BAT_PATH%%BAT_ID%.BAT END   >> %LOGFILE%
echo %date% %time% >> %LOGFILE%
echo %LINE01% >> %LOGFILE%

rem バッチ終了コード
if %STATUS% GTR 0 (
	set STATUS=9
)

echo %BAT_ID% 終了コード＝%STATUS% >> %LOGFILE%

exit /b %STATUS%
rem ===========================================================================

rem ---------------- End Of Batch ---------------------------------------------
 
 /////////////////////////////////////サンプルソース：終了////////////////////////////////////////////
 
